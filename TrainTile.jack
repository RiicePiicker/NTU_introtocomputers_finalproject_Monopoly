class TrainTile {

    field int basePrice; 
    field Player owner;

    constructor TrainTile new(int price) {
        let basePrice = price;
        let owner = null;
        return this;
    }

    method void print(String msg, int row) {
        do Output.moveCursor(7+row, 7);
        do Output.printString(msg);
        do msg.dispose();
        return;
    }

    method void setOwner(Player p) {
        let owner = p;
        return;
    }

    method Player getOwner() {
        return owner;
    }

    method int calculateRent() {
        var int numOwnedTrains;
        var int rent;
        
    
        let numOwnedTrains = owner.getTrains(); 
        
      
        if (numOwnedTrains = 1) {
            let rent = 25;
        }
        if (numOwnedTrains = 2) {
            let rent = 50;
        }
        if (numOwnedTrains = 3) {
            let rent = 100;
        }
        if (numOwnedTrains = 4) {
            let rent = 200;
        }
        
        return rent;
    }

    method void activate(Player p, Board board) {
        var int rentDue;
        var int buy;
        
        if (owner = null) {
            do Ui.printTraNull(basePrice);

            while(~(buy = 89) & ~(buy = 121) & ~(buy = 78) & ~(buy = 110)){
                let buy = Keyboard.keyPressed();
            }
            do Sys.wait(25);
            if (buy = 89 | buy = 121){
                do p.buyTrain(this, basePrice, board);
            }
            do Sys.wait(100);
            return;
        } 
        
        if (~(owner = p)) { 
            let rentDue = calculateRent(); 

            do Ui.printTraPay(rentDue,owner.getID() + 1 );
            do Sys.wait(25);
            do p.pay(rentDue, board);
            do owner.receive(rentDue);
            do Sys.wait(300);
        }

        if (owner = p) {
            do Ui.printTraself();
            do Sys.wait(300);
        }

        return;
    }
     method void resetOwnerIfMatch(Player p) {
        if (owner = p) {
            let owner = null; 
        }
        return;
    }
}
