class PropertyTile {

    field int price;
    field int baseRent;
    field int houses;        
    field int houseCost;
    field Player owner;
    static String prtself31;
    static String prtself32;
    static String prtself4;
    static String prtself5;
    static String prtself6;
    static String prtself7;
    
    constructor PropertyTile new(int p, int r, int h) {
        let price = p;
        let baseRent = r;
        let houseCost = h;
        let houses = 0;
        let owner = null;
        return this;
    }

    function void init(){
        let prtself31 = "You have ";
        let prtself32 = " houses here";
        let prtself4 = "A new one costs $";
        let prtself5 = "Press Y to build one";
        let prtself6 = "Press N to skip it";
        let prtself7 = "You can't build more";
        return;
    }

    method void activate(Player p, Board board) {
        var int rentDue;
        var int buy;
        let buy = 0;
        do Sys.wait(50);

        // No owner, attempt to buy
        if (owner = null) {
            do Ui.printProNull(price);
            while(~(buy = 89) & ~(buy = 121) & ~(buy = 78) & ~(buy = 110)){
                let buy = Keyboard.keyPressed();
            }
            do Sys.wait(25);
            if (buy = 89 | buy = 121){
                do Sys.wait(25);
                do p.buyProperty(this, price, board);
            }
            return;
        }

        // Owned by someone else, pay rent
        if (~(owner = p)) {
            let rentDue = getRent();
            do Ui.printProPay(rentDue, owner.getID() + 1);
            do Sys.wait(300);
            
            do p.pay(rentDue, board);
            do owner.receive(rentDue);
            return;
        }

        // Owned by self, nothing happens
        if (owner = p) {
            do Ui.printProself();
            do Output.moveCursor(9, 7);
            do Output.printString(prtself31);
            do Output.printInt(houses);
            do Output.printString(prtself32);
            do Sys.wait(25);
            if( houses < 5 ){
                do Output.moveCursor(10, 7);
                do Output.printString(prtself4);
                do Output.printInt(houseCost);
                do Output.moveCursor(11, 7);
                do Output.printString(prtself5);
                do Output.moveCursor(12, 8);
                do Output.printString(prtself6);
                let buy = 0;
                while(~(buy = 89) & ~(buy = 121) & ~(buy = 78) & ~(buy = 110)){
                    let buy = Keyboard.keyPressed();
                }
                do Sys.wait(25);
                if (buy = 89 | buy = 121){
                    do p.buildHouse(this, board);
                }
            }
            else {
                do Output.moveCursor(10, 7);
                do Output.printString(prtself7);
                do Sys.wait(300);
            }
        }
        return;
    }

    method void addHouse() {
        if (houses < 5) {
            let houses = houses + 1;
        }
        return;
    }

    method int getRent() {
        if (houses = 0) {
            return baseRent;
        }
        if (houses = 1) {
            return baseRent * 2;
        }
        if (houses = 2) {
            return baseRent * 3;
        }
        if (houses = 3) {
            return baseRent * 4;
        }
        if (houses = 4) {
            return baseRent * 5;
        }
        
        return baseRent * 6;
    }

    method int getHouseCost() {
        return houseCost;
    }

    method Player getOwner() {
        return owner;
    }

    method void setOwner(Player p) {
        do Sys.wait(25);
        let owner = p;
        return;
    }
    method void resetOwnerIfMatch(Player p) {
        if (owner = p) {
            let owner = null;
            let houses = 0;  
        }
        return;
    }
}
