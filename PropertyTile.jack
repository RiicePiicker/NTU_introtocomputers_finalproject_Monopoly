class PropertyTile {

    field int price;
    field int baseRent;
    field int houses;        
    field int houseCost;
    field Player owner;
    static String prtnull1;
    static String prtnull2;
    static String prtnull3;
    static String prtnull4;
    static String prtnull5;
    static String prtother1;
    static String prtother2;
    static String prtother3;
    static String prtself1;
    static String prtself2;
    static String prtself31;
    static String prtself32;
    static String prtself4;
    static String prtself5;
    static String prtself6;
    static String prtself7;
    
    constructor PropertyTile new(int p, int r, int h) {
        let price = p;
        let baseRent = r;
        let houseCost = h;
        let houses = 0;
        let owner = null;
        return this;
    }

    function void init(){
        let prtnull1 = "This is a unclaimed";
        let prtnull2 = "property land";
        let prtnull3 = "its price is $";
        let prtnull4 = "Press Y to buy it";
        let prtnull5 = "Press N to skip it";
        let prtother1 = "This property";
        let prtother2 = "is owned by player ";
        let prtother3 = "You need to pay $";
        let prtself1 = "This property";
        let prtself2 = "is owned by yourself";
        let prtself31 = "You have ";
        let prtself32 = " houses here";
        let prtself4 = "A new one costs $";
        let prtself5 = "Press Y to build one";
        let prtself6 = "Press N to skip it";
        let prtself7 = "You can't build more";
        return;
    }

    method void activate(Player p, Board board) {
        var int rentDue;
        var int buy;
        let buy = 0;
        do Sys.wait(50);

        // No owner, attempt to buy
        if (owner = null) {
            do Output.moveCursor(7, 7);
            do Output.printString(prtnull1);
            do Output.moveCursor(8, 9);
            do Output.printString(prtnull2);
            do Output.moveCursor(9, 7);
            do Output.printString(prtnull3);
            do Output.printInt(price);
            do Output.moveCursor(10, 7);
            do Output.printString(prtnull4);
            do Output.moveCursor(11, 7);
            do Output.printString(prtnull5);
            while(~(buy = 89) & ~(buy = 121) & ~(buy = 78) & ~(buy = 110)){
                let buy = Keyboard.keyPressed();
            }
            do Sys.wait(25);
            if (buy = 89 | buy = 121){
                do Sys.wait(25);
                do p.buyProperty(this, price, board);
            }
            return;
        }

        // Owned by someone else, pay rent
        if (~(owner = p)) {
            let rentDue = getRent();
            do Output.moveCursor(7, 9);
            do Output.printString(prtother1);
            do Output.moveCursor(8, 7);
            do Output.printString(prtother2);
            do Output.printInt(owner.getID() + 1);
            do Output.moveCursor(9, 7);
            do Output.printString(prtother3);
            do Output.printInt(rentDue);
            do Sys.wait(300);
            do p.pay(rentDue, board);
            do owner.receive(rentDue);
            return;
        }

        // Owned by self, nothing happens
        if (owner = p) {
            do Output.moveCursor(7, 9);
            do Output.printString(prtself1);
            do Output.moveCursor(8, 7);
            do Output.printString(prtself2);
            do Output.moveCursor(9, 7);
            do Output.printString(prtself31);
            do Output.printInt(houses);
            do Output.printString(prtself32);
            do Sys.wait(25);
            if( houses < 5 ){
                do Output.moveCursor(10, 7);
                do Output.printString(prtself4);
                do Output.printInt(houseCost);
                do Output.moveCursor(11, 7);
                do Output.printString(prtself5);
                do Output.moveCursor(12, 8);
                do Output.printString(prtself6);
                let buy = 0;
                while(~(buy = 89) & ~(buy = 121) & ~(buy = 78) & ~(buy = 110)){
                    let buy = Keyboard.keyPressed();
                }
                do Sys.wait(25);
                if (buy = 89 | buy = 121){
                    do p.buildHouse(this, board);
                }
            }
            else {
                do Output.moveCursor(10, 7);
                do Output.printString(prtself7);
                do Sys.wait(300);
            }
        }
        return;
    }

    method void addHouse() {
        if (houses < 5) {
            let houses = houses + 1;
        }
        return;
    }

    method int getRent() {
        if (houses = 0) {
            return baseRent;
        }
        if (houses = 1) {
            return baseRent * 2;
        }
        if (houses = 2) {
            return baseRent * 3;
        }
        if (houses = 3) {
            return baseRent * 4;
        }
        if (houses = 4) {
            return baseRent * 5;
        }
        
        return baseRent * 6;
    }

    method int getHouseCost() {
        return houseCost;
    }

    method Player getOwner() {
        return owner;
    }

    method void setOwner(Player p) {
        do Sys.wait(25);
        let owner = p;
        return;
    }
}
